import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    dependencies {
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:1.7.20")
    }
}

plugins {
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id "org.jetbrains.dokka" version "1.7.10" apply false
    id "io.gitlab.arturbosch.detekt" version "1.22.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Detekt report merging
tasks.register("reportMerge", ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.sarif")
}

subprojects {
    // Detekt
    detekt {
        buildUponDefaultConfig = true
    }

    tasks.withType(Detekt).configureEach {
        jvmTarget = "1.8"
        basePath = rootProject.projectDir.absolutePath

        reports {
            sarif.enabled = true
            // workaround for https://github.com/detekt/detekt/issues/4192
            sarif.destination = project.file("build/reports/detekt/detekt.sarif")
        }
        finalizedBy(reportMerge)

        reportMerge.configure { mergeTask ->
            mergeTask.input.from(sarifReportFile)
        }
    }

    tasks.withType(DetektCreateBaselineTask).configureEach {
        jvmTarget = "1.8"
    }
}
